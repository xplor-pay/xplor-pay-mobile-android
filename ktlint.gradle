// kotlin-gradle-plugin must be applied for configuration below to work
// (see https://kotlinlang.org/docs/reference/using-gradle.html)

configurations {
    ktlint
}


dependencies {
    ktlint('com.pinterest:ktlint:0.50.0') {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

tasks.register("ktlintVerfification", JavaExec) {
    group = "verification"
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    jvmArgs = ["--add-opens=java.base/java.lang=ALL-UNNAMED"]
    args "src/**/*.kt"
    // See https://pinterest.github.io/ktlint/install/cli/#command-line-usage for more information
}

tasks.register("ktlintFormatting", JavaExec) {
    group = "formatting"
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    jvmArgs = ["--add-opens=java.base/java.lang=ALL-UNNAMED"]
    args "-F", "src/**/*.kt"
    // see https://pinterest.github.io/ktlint/install/cli/#command-line-usage for more information
}